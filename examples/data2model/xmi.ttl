@prefix elmo: <http://bp4mc2.org/elmo/def#>.
@prefix html: <http://www.w3.org/1999/xhtml/vocab#>.
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>.
@prefix dcterms: <http://purl.org/dc/terms/>.
@prefix void: <http://rdfs.org/ns/void#>.
@prefix geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>.
@prefix stage: <http://localhost:8080/data2model/stage#>.

stage:Xmi a elmo:Representation;
  elmo:url-pattern "/query/uml(|\.xmi)$";
  elmo:query '''
    PREFIX skosthes: <http://purl.org/iso25964/skos-thes#>
    PREFIX skoslex: <http://bp4mc2.org/def/skos-lex#>
    PREFIX mim: <http://bp4mc2.org/def/mim#>
    CONSTRUCT {
      ?begrip a mim:Objecttype.
      ?begrip rdfs:label ?begrip_label.
      ?begrip mim:definitie ?definition.
      ?genid a mim:Generalisatie.
      ?genid mim:objecttype ?special.
      ?genid mim:gerelateerdObjecttype ?general.
      ?compid a mim:Relatiesoort.
      ?compid mim:typeAggregatie mim:Composite.
      ?compid mim:objecttype ?whole.
      ?compid mim:gerelateerdObjecttype ?part.
      ?relid a mim:Relatiesoort.
      ?relid mim:objecttype ?source.
      ?relid mim:naam ?relname.
      ?relid mim:gerelateerdObjecttype ?target.
    }
    WHERE {
      GRAPH <http://localhost:8080/data2model/container/data> {
        {
          ?begrip a skos:Concept.
          ?begrip rdfs:label ?begrip_label.
          OPTIONAL {?begrip skos:definition ?definition}
        }
        UNION
        {
          SELECT ?source (iri(concat(str(?source),'#REL#',str(?target))) as ?relid) ?target (if(?rel=<http://bp4mc2.org/def/skos-lex#isAbout>,'betreft',strafter(str(?rel),'#')) as ?relname)
          WHERE {
            ?source ?rel ?target
            FILTER (?rel = skoslex:agent || ?rel = skoslex:actor || ?rel = skoslex:object || ?rel = skoslex:record || ?rel = skoslex:isAbout)
          }
        }
        UNION
        {
          SELECT ?part (iri(concat(str(?whole),'#COMP#',str(?part))) as ?compid) ?whole
          WHERE {
            {
              ?part skosthes:broaderPartitive ?whole
            }
            UNION
            {
              ?whole skosthes:narrowerPartitive ?part
            }
          }
        }
        OPTIONAL {
          SELECT ?special (iri(concat(str(?special),'#G#',str(?general))) as ?genid) ?general
          WHERE {
            {
              ?special skosthes:broaderGeneric ?general
            }
            UNION
            {
              ?general skosthes:narrowerGeneric ?special
            }
          }
        }
      }
    }
  ''';
.

stage:YedActs a elmo:Representation;
  elmo:url-pattern "/query/acts.yed$";
  elmo:contains stage:Menu;
  elmo:query '''
    PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
    PREFIX skosthes: <http://purl.org/iso25964/skos-thes#>
    PREFIX skoslex: <http://bp4mc2.org/def/skos-lex#>
    CONSTRUCT {
      ?act rdfs:label ?actlabel.
      ?act a skoslex:Act.
      ?act rdfs:comment ?comment.
      ?act skoslex:record ?record.
      ?record rdfs:label ?recordlabel.
      ?record a skoslex:Record.
      ?record rdfs:comment ?recordcomment.
      skoslex:record rdfs:label "vastlegging".
    }
    WHERE {
      SELECT ?act ?actlabel ?record ?recordlabel
          (CONCAT(
            "betreft: \\n", GROUP_CONCAT(DISTINCT CONCAT("- ",?objectlabel); SEPARATOR = "\\n"),
            "\\n betrokkenen: \\n", GROUP_CONCAT(DISTINCT CONCAT("- ",?actorlabel); SEPARATOR = "\\n"),
            "\\n beroepsgroep: \\n", GROUP_CONCAT(DISTINCT CONCAT("- ",?agentlabel); SEPARATOR = "\\n")
          ) as ?comment)
          (CONCAT(
            "gaat over: \\n", GROUP_CONCAT(DISTINCT CONCAT("- ",?aboutlabel); SEPARATOR = "\\n")
          ) as ?recordcomment)
      WHERE {
        GRAPH <http://localhost:8080/data2model/container/data> {
          ?act a skoslex:Act.
          ?act rdfs:label ?actlabel.
          OPTIONAL {
            ?act skoslex:object ?object.
            ?object rdfs:label ?objectlabel
          }
          OPTIONAL {
            ?act skoslex:actor ?actor.
            ?actor rdfs:label ?actorlabel
          }
          OPTIONAL {
            ?act skoslex:agent ?agent.
            ?agent rdfs:label ?agentlabel
          }
          ?act skoslex:record ?record.
          ?record rdfs:label ?recordlabel
          OPTIONAL {
            ?art skosthes:broaderPartitive* ?record.
            ?art skoslex:isAbout ?about.
            ?about rdfs:label ?aboutlabel
          }
        }
      }
      GROUP BY ?act ?actlabel ?record ?recordlabel
    }
  ''';
.
